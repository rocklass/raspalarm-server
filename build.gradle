import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import org.apache.tools.ant.filters.ReplaceTokens

apply from: "buildsystem/dependencies.gradle"

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.5.3.RELEASE"
        classpath "com.bmuschko:gradle-docker-plugin:3.0.7"
    }
}

group = appGroup

apply plugin: "java"
apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "org.springframework.boot"
apply plugin: "com.bmuschko.docker-remote-api"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

loadConfiguration()

jar {
    baseName = appName
    version = appVersion
}

dependencies {
    compile     projectDependencies.fcmJavaCore
    compile     projectDependencies.fcmJavaClient
    compile     projectDependencies.springBootStarterWeb
    testCompile projectTestDependencies.springBootStarterTest
}

docker {
    url = project.dockerUrl
}

def loadConfiguration() {
    def environment = hasProperty("env") ? env : "dev"

    Properties properties = new Properties()
    properties.load(project.file("src/main/profile/$environment/env.properties").newDataInputStream())
    properties.each { property ->
        project.ext.set(property.key, property.value)
    }
}

task dockerBuildImage(type: DockerBuildImage, dependsOn: ":processDockerRessources") {
    inputDir = project.file("build/docker")
    tag = "$appGroup/$appName"
}

task processDockerRessources(type: Copy, dependsOn: ":build") {
    from ("src/main/docker") {
        filter(ReplaceTokens, tokens: [
                dockerContainerFrom: project.dockerContainerFrom,
                dockerContainerJarName: jar.baseName,
                dockerContainerJarVersion: jar.version
        ])
    }
    from ("build/libs") {
        include("*.jar")
    }
    into "build/docker"
}